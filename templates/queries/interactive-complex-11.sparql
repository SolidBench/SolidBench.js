# Job referral
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX sn: <http://localhost:3000/www.ldbc.eu/ldbc_socialnet/1.0/data/>
PREFIX snvoc: <http://localhost:3000/www.ldbc.eu/ldbc_socialnet/1.0/vocabulary/>
PREFIX sntag: <http://localhost:3000/www.ldbc.eu/ldbc_socialnet/1.0/tag/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX dbpedia: <http://localhost:3000/dbpedia.org/resource/>
PREFIX dbpedia-owl: <http://localhost:3000/dbpedia.org/ontology/>

SELECT
    (?frId AS ?personId)
    (?first AS ?personFirstName)
    (?last AS ?personLastName)
    (?orgName AS ?organizationName)
    (?startYear AS ?organizationWorkFromYear)
WHERE {
    {
        SELECT DISTINCT
            ?fr
            ?frId
        WHERE
        {
            ?rootPerson a snvoc:Person .
            ?rootPerson snvoc:id ?rootId .
            ?rootPerson ((snvoc:knows/snvoc:hasPerson)|^(snvoc:knows/snvoc:hasPerson))?/((snvoc:knows/snvoc:hasPerson)|^(snvoc:knows/snvoc:hasPerson)) ?fr .
            ?fr snvoc:id ?frId .
            FILTER(?frId != ?rootId)
        }
    } .
    ?fr snvoc:workAt ?work .
    ?work snvoc:workFrom ?startYear .
    FILTER (?startYear < ?workFromYear) .
    ?work snvoc:hasOrganisation ?org .
    ?org foaf:name ?orgName .
    ?org snvoc:isLocatedIn ?country.
    ?country foaf:name ?countryName .
    ?fr snvoc:firstName ?first .
    ?fr snvoc:lastName ?last .
}
ORDER BY ?startYear ?frId DESC(?orgName)
LIMIT 10
